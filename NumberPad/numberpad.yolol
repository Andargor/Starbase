e=:ent ife then:dbg=:entry :ent=0end goto2+2*((e+:clr)>0)
goto1+2*((:1+:2+:3+:4+:5+:6+:7+:8+:9+:0)>0)
num*=10 num+=:1+:2+:3+:4+:5+:6+:7+:8+:9 
:1=0 :2=0 :3=0 :4=0 :5=0 :6=0 :7=0 :8=0 :9=0 :0=0
:entry=(1-:clr)*num*:neg/:dec goto1+5*((:clr+:ent)>0)
num=0 :neg=1 :clr=0 :dec=1 goto1

// :1 to :0 are number buttons first field name.  The On value is the
// number (i.e. :5 on value is 5), the off value is 0.
// :ent :clr are enter and clear buttons first field name.  On value
// is 1, off value is 0.
// :neg is the negative button first field name.  On value is -1, off
// value is 1
// :dec is the decimal button first field name.  On value is 1000, off
// value is 1
// :Entry is the number panel display.  :dbg is the variable the final
// number will be assigned to, rename this as needed.
// To use: press negative or decimals if needed. Enter the numbers one
// at a time, making sure they appear on the Entry panel. When
// complete hit Enter button to assign it to your :dbg variable.